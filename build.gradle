buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'
if (project.file('../gradletools.gradle').exists()) {
    apply from: '../gradletools.gradle'
} else {
    apply from: 'https://raw.githubusercontent.com/McJtyMods/MultiWorkspace/1.19_tech/gradletools.gradle'
}

repositories {
    maven { url "https://dvs1.progwml6.com/files/maven" } // JEI
    maven { url "https://maven.k-4u.nl" } // McJtyLib, TOP
}

group = 'com.mcjty.signtastic'
archivesBaseName = 'signtastic'
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

runs('signtastic')
jars('signtastic')

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    mc()
    jei()
    top()
    mcjtylib()
}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.curseforge.enabled = project.hasProperty('curseforge_key')
cfdeps(['the-one-probe'], ['mcjtylib'], ['1.19.3'])
